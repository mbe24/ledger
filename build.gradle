buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
    }
}

subprojects {
    group 'org.beyene.ledger'
    version '1.0.3'

    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:all" << "-Werror"
        }
    }

    dependencies {
        testCompile 'junit:junit:4.12', {
            exclude group: 'org.hamcrest', module: 'hamcrest-core'
        }
        testCompile 'org.mockito:mockito-core:2.22.0', {
            exclude group: 'org.hamcrest', module: 'hamcrest-core'
        }
        testCompile 'org.hamcrest:hamcrest-all:1.3'
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        //archives javadocJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                //artifact javadocJar
            }
        }
    }

    bintray {
        user = bintray_user // this usually comes from gradle.properties file in ~/.gradle
        key = bintray_api_key // this usually comes from gradle.properties file in ~/.gradle

        publications = ['mavenJava'] // When uploading Maven-based publication files

        dryRun = false // Whether to run this as dry-run, without deploying
        publish = true // If version should be auto published after an upload
        pkg {
            repo = 'ledger'
            name = 'ledger'
            version {
                name = project.version // Bintray logical version name
            }
        }


    }
}
